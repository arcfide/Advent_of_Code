:Namespace AoC_2023

⍝ The code here represents the more or less exact code that I used to
⍝ solve each Advent of Code problem for 2023.
⍝
⍝ The goal here is not to give you the most elegant solutions, but to
⍝ instead give you an idea of the sort of rough draft code that would
⍝ be written when I am under time pressures and trying to get points on
⍝ the board. This should help me get some very interesting data on the
⍝ use of APL for myself under time pressures and under conditions where
⍝ I am not going to be refining the code over time or doing any
⍝ refactoring.

day1_p1←{+⌿(⍎2⍴1∘↑,¯1∘↑)¨⎕D(∊⍨⊆⊢)⍵∩⎕D,⎕UCS 13}
day1_p2←{day1_p1 txt⊣'123456789'{(⍵⌿txt)←⍺}¨'one' 'two' 'three' 'four' 'five' 'six' 'seven' 'eight' 'nine'⍷¨⊂txt←⍵}

day2_p1←{
	red green blue←⍳3
	games←{((⌈⌿+/),⌈⌿)↑{(⊃∘⌽¨⍵)@(⊃¨⍵)⊢0 0 0}¨⍵}⍤1↑⍎¨¨¨¨' '(⌽≠⊆⊢)¨¨¨','(≠⊆⊢)¨¨';'(≠⊆⊢)¨':'(⊢↓⍨1+⍳⍨)¨5↓¨(⎕UCS 13)(≠⊆⊢)⍵
	+⌿1+⍸games∧.≤(+⌿,⊢),12 13 14
}

day2_p2←{
	red green blue←⍳3
	games←{((⌈⌿+/),⌈⌿)↑{(⊃∘⌽¨⍵)@(⊃¨⍵)⊢0 0 0}¨⍵}⍤1↑⍎¨¨¨¨' '(⌽≠⊆⊢)¨¨¨','(≠⊆⊢)¨¨';'(≠⊆⊢)¨':'(⊢↓⍨1+⍳⍨)¨5↓¨(⎕UCS 13)(≠⊆⊢)⍵
	+⌿×/1↓[1]games
}

day3_p1←{
	adj←∨/∨/~(⎕D,'. ')∊⍨{⍵}⌺3 3⊢⍵
	msk←⍵∊⎕D
	+⌿⍎¨(∨/¨msk⊆⍥,adj)⌿msk⊆⍥,⍵
}

day3_p2←{
	msk←⍵∊⎕D
	gears←⌈/⌈/{⍵}⌺3 3⊢('*'=⍵)×(⍴⍵)⍴⍳≢,⍵
	gear_num←⌈/¨msk⊆⍥,gears
	nums←⍎¨msk⊆⍥,⍵
	+⌿gear_num{0=⍺:0 ⋄ 2≠≢⍵:0 ⋄ ×⌿⍵}⌸nums
}

day4_p1←{
	f←{0=x←+⌿⍵∊⍺:0 ⋄ 2*x-1}
	+⌿⍎⍤1{'f'@{'|'=⍵}⊢⍵}':'(⊢↓⍨2+⍳⍨)⍤1⊢⍵
}

day4_p2←{
	f←{+⌿⍵∊⍺}
	matches←⍎⍤1{'f'@{'|'=⍵}⊢⍵}':'(⊢↓⍨2+⍳⍨)⍤1⊢⍵
	1⊃{c t←⍵ ⋄ nc←∊c{1+⍺+⍳⍵}¨matches[c] ⋄ nc(t+≢nc)}⍣{0≡≢⊃⍺}(⍳≢matches)(≢matches)
}

day5_p1←{
	maps←⍎⍤1¨2↓¨(⍵∧.=' ')⊂[0]⍵
	seeds←⍎7↓0⌷⍵
	lookup←{⍵{m←¯1=⍵ ⋄ (⍺×m)+⍵×~m}⌈/⍵{d l r←⍵ ⋄ h←l+r ⋄ (l≤⍺)∧⍺≤h:d+⍺-l ⋄ ¯1}⍤1⍤0 2⊢⍺}
	⌊⌿⊃lookup⌿(⌽maps),⊂seeds
}

day5_p2←{
	maps←{d s r←⍎⍵ ⋄ 2 2⍴s(s+r)d(d+r)}⍤1¨2↓¨(⍵∧.=' ')⊂[0]⍵
	seeds←(⊣/,[.5]+/){⍵}⌺(⍪2 2)⍎7↓0⌷⍵
	norm←{
		src←{⍵[⍋⍵]},⍺[;0;]
		{
			beg←⍵[;0] ⋄ end←⍵[;1]
			i←⍸⊃≠/bi ei←src∘⍸¨beg(end-1)
			end,←end[i] ⋄ beg,←end[i]←src[ei[i]]
			beg,⍪end
		}⍣≡⊢⍵
	}
	lookup←{
		src dst←⊂[0 2]⍺ ⋄ i←⍋src ⋄ src←,src[i;] ⋄ dst←,dst[i;]
		beg end←↓⍉⍺ norm ⍵ ⋄ len←end-beg
		i←⍸0=2|bi←src⍸beg ⋄ beg[i]←dst[bi[i]]+beg[i]-src[bi[i]] ⋄ end[i]←beg[i]+len[i]
		beg,⍪end
	}
	⌊⌿0⌷⍉⊃lookup⌿(⌽maps),⊂seeds
}

day6_p1←{
	time dist←↓{⍎(1+⍵⍳':')↓⍵}⍤1⊢⍵
	×⌿+⌿¨dist{⍺<(⊢×⍵-⊢)⍳⍵}¨time
}

day6_p2←{
	time dist←{⍎' '~⍨(1+⍵⍳':')↓⍵}⍤1⊢⍵
	+⌿dist<(⊢×time-⊢)⍳time
}

day7_p1←{
	hands bids←↓⍉' '(≠⊆⊢)⍤1⊢⍵
	bids←⍎¨bids
	hands←'23456789TJQKA'⍳↑hands
	type←{
		1≡≢h←{≢⍵}⌸⍵:6
		4≡⌈⌿h:5
		2≡≢h:4
		3≡⌈⌿h:3
		3≡≢h:2
		4≡≢h:1
		0
	}
	ht←type⍤1⊢hands
	rank←1+⍋⍋ht,hands
	rank+.×bids
}

day7_p2←{
	hands bids←↓⍉' '(≠⊆⊢)⍤1⊢⍵
	bids←⍎¨bids
	hands←'J23456789TQKA'⍳↑hands
	type←{
		c←5↑c,5⍴⊃⊢/{⍵[⍒⍵;]}{(≢⍵)⍺}⌸c←⍵~0
		1≡≢h←{≢⍵}⌸c:6
		4≡⌈⌿h:5
		2≡≢h:4
		3≡⌈⌿h:3
		3≡≢h:2
		4≡≢h:1
		0
	}
	ht←type⍤1⊢hands
	rank←1+⍋⍋ht,hands
	rank+.×bids
}

day8_p1←{
	steps←(0⌷⍵)~' '
	map←{(⍵∊⎕A)⊆⍵}⍤1⊢2↓⍵
	0{
		⍵≡'ZZZ':⍺
		(1+⍺)∇⊃map[map[;0]⍳⊂⍵;1 2]['LR'⍳steps[(≢steps)|⍺]]
	}'AAA'
}

day8_p2←{
	steps←(0⌷⍵)~' '
	map←{(⍵∊⎕A,⎕D)⊆⍵}⍤1⊢2↓⍵
	nxt←{⊃map[map[;0]⍳⊂⍵;1 2]['LR'⍳steps[(≢steps)|⍺]]}
	cnt←{⍺←0 ⋄ 'Z'=⊃⌽⍵:⍵ ⍺ ⋄ (1+⍺)∇ ⍺ nxt ⍵}
	⎕←↑z c1←↓⍉↑cnt¨map[;0]⌿⍨'A'=⊃∘⌽¨map[;0]
	∧⌿c1-⍨1⌷⍉↑(1+c1)cnt¨c1 nxt¨z
}

day9_p1←{
	h←{⍎'-'⎕R'¯'⊢⍵}⍤1⊢⍵
	+⌿{⍺←⊃⌽⍵ ⋄ ∧⌿0=⍵:+⌿⍺ ⋄ x←2-⍨⌿⍵ ⋄ (⍺,⊃⌽x)∇ x}⍤1⊢h
}

day9_p2←{
	h←{⍎'-'⎕R'¯'⊢⍵}⍤1⊢⍵
	+⌿{⍺←⊃⍵ ⋄ ∧⌿0=⍵:-⌿⍺ ⋄ x←2-⍨⌿⍵ ⋄ (⍺,⊃x)∇ x}⍤1⊢h
}

day10_p1←{
	⍝ '.S|-LJ7F'
	N←¯1 0 ⋄ S←1 0 ⋄ W←0 ¯1 ⋄ E←0 1
	s←(⍬ ⍬(N S)(E W)(N E)(W N)(W S)(S E))
	adj←(⊂¨⍳⍴⍵)+s['.S|-LJ7F'⍳⍺@{'S'=⍵}⍵]
	start←⍸'S'=⍵
	0{
		front prev←⍵
		front←(⊃⍪⌿adj[front])~prev,next←front
		≡⌿front:1+⍺
		(1+⍺)∇ front next
	}start ⍬
}
 
day10_p2←{
	⍝ '.S|-LJ7F'
	N←¯1 0 ⋄ S←1 0 ⋄ W←0 ¯1 ⋄ E←0 1
	s←(⍬ ⍬(N S)(E W)(N E)(W N)(W S)(S E))
	adj←(⊂¨⍳⍴⍵)+s['.S|-LJ7F'⍳tm←⍺@{'S'=⍵}⍵]
	loop←{⍵∪⊃⍪/adj[⍵]}⍣≡⍸'S'=⍵
	tm←(1 0⍴⍨2×≢⍉tm)\(1 0⍴⍨2×≢tm)⍀'.'@((,⍳⍴tm)~loop)⊢tm
	exp←{nw n ne w c e sw s se←,⍵
		c≠' ':c
		(n∊'F7|')∨s∊'|LJ':'|' ⋄ (w∊'FL-')∨e∊'7J-':'-'
		c
	}
	map←(⊢+2×~)' .'∊⍨tm←exp⌺3 3⊢tm
	+⌿,(tm='.')∧{⍵×(⍵=1)⍲∨/∨/0={⍵}⌺3 3⊢⍵}⍣≡⊢map
}

day11_p1←{
	xu←(1+'.'∧.=⍉⍵)⌿(1+'.'∧.=⍵)/⍵
	+⌿{+⌿|⍺-⍵}/↑(~∨\∘.=⍨⍳≢p)/⍥,p←∘.,⍨⊂¨⍸xu='#'
}

day11_p2←{
	ys←'.'∧.=⍉⍵ ⋄ xs←'.'∧.=⍵ ⋄ age←1000000
	s←(+⍀(age-1)×ys)∘.,+⍀(age-1)×xs
	p←↑↑(~∨\∘.=⍨⍳≢p)/⍥,p←∘.,⍨⊂¨(,⍵='#')⌿,s+⍳⍴⍵
	+⌿+/|-/[1]p
}
 
day12_p1←{
	a b←↓⍉' '(≠⊆⊢)⍤1⊢⍵
	a←¯1+'?.#'∘⍳¨a
	b←⍎¨b
	+⌿a{+⌿⍵{⍺≡≢¨⊆⍨⍵}⍤1⊢⍉(0⌈⍺)+[0]m⍀(x⍴2)⊤⍳2*x←+⌿m←¯1=⍺}¨b
}

day12_p2←{
	xa←{⊃{⍺,'?',⍵}⌿⍺⍴⊂⍵} ⋄ xb←{∊⍺⍴⊂⍵}
	a b←↓⍉' '(≠⊆⊢)⍤1⊢⍵ ⋄ b←⍎¨b
	mem←⍬ ⋄ res←⍬ ⋄ save←{res,←⍵ ⋄ mem,←⊂⍺ ⋄ ⍵}
	cnt←{
		(≢mem)>i←mem⍳⊂id←⍺ ⍵:i⊃res ⋄ map siz←⍵ ⋄ c←⊃map
		0=≢map                        :id save ((⍺=0)∧(0=≢siz))∨(⍺=⊃siz)∧1=≢siz
		(⍺=0)∧(≢map)<(+⌿siz)+(≢siz)-1 :id save 0
		(0=≢siz)∧'#'∊map              :id save 0
		(0=≢siz)                      :id save 1
		(c='.')∧⍺=0                   :id save 0 ∇ (map↓⍨0⍳⍨'.'=map)siz
		(c='.')∧⍺=n←⊃siz              :id save 0 ∇ (map↓⍨0⍳⍨'.'=map)(1↓siz)
		(c='.')                       :id save 0
		(c='#')∧(0=≢siz)              :id save 0
		(c='#')∧(⍺≠0)                 :id save 0
		(c='#')∧n>m←0⍳⍨map∊'?#'       :id save 0
		(c='#')                       :id save n ∇(n↓map)siz
		(⍺=n)                         :id save 0 ∇(1↓map)(1↓siz)
		('#'∊m↑map)∧n>m               :id save 0
		n>m                           :id save 0 ∇(m↓map)siz
		1                             :id save (0 ∇(1↓map)siz)+n∇(n↓map)siz
	}
	+⌿(5 xa¨ a){(⍳≢⍺){0 cnt ⍵⊣mem⊢←⍬⊣res⊢←⍬}⍤0 1⍉↑⍺ ⍵}5 xb¨ b
}

day13_p1←{
	in←'.#'∘⍳¨{⍵~' '}⍤1¨1↓¨d⊂[0]⍨∧/' '=d←' '⍪⍵
	rpt←{ax←⍺ ⋄ s←(⍴⍵)[ax] ⋄ c←1+⍳s-1 ⋄ w←c⌊s-c ⋄ I←⍵∘{(⊂⍵)⌷[ax]⍺}
		s|1+1⍳⍨c{(⊢≡⌽[ax])I(⍺-⍵)+⍳2×⍵}¨w}
	+⌿(100×0 rpt¨in),1 rpt¨in
}
 
day13_p2←{
	in←'.#'∘⍳¨{⍵~' '}⍤1¨1↓¨d⊂[0]⍨∧/' '=d←' '⍪⍵
	rpt←{ax←⍺ ⋄ s←(⍴⍵)[ax] ⋄ c←1+⍳s-1 ⋄ w←c⌊s-c ⋄ I←⍵∘{(⊂⍵)⌷[ax]⍺}
		c{(⊢=⌽[ax])I(⍺-⍵)+⍳2×⍵}¨w
	}
	+⌿{∨⌿m←2=+⌿∘,¨0=0 rpt ⍵:100×1+1⍳⍨m ⋄ ∨⌿m←2=+⌿∘,¨0=1 rpt ⍵:1+1⍳⍨m}¨in
}
 
day14_p1←{
	+⌿{+⌿∊(≢⍵)-(1+⍸r)+(⍳+⌿)¨'O'=(r←'#'=⍵)⊂⍵}⍤1⊢m←⍉'#'⍪⍵
}

day14_p2←{
	m←'#',('#'⍪⍵⍪'#'),'#'
	tilt←{{∊{⍵[⍋'#O.'⍳⍵]}¨('#'=⍵)⊂⍵}⍤1⊢⍵}
	cycle←{⊖⌽tilt⍉⊖tilt⍉⊖tilt⍉tilt⍉⍵}
	load←{+⌿,m×(≢⍵)-1+(⍳≢⍵)×⍤1⊢m←'O'=⍉⍵}
	n←cycle⍣{(⊂⍺)∊seen:1 ⋄ 0⊣seen,←⊂⍺}⊃seen←,⊂m
	cnt←1000000000
	load seen⊃⍨h+cnt-h+loop×⌊(cnt-h)÷loop←(≢seen)-h←seen⍳⊂n
}

day15_p1←{
	hash←{{256|17×⍺+⍵}⌿(⌽⎕UCS ⍵),0}
	+⌿hash¨','(≠⊆⊢)⍵
}

day15_p2←{
	hash←{{256|17×⍺+⍵}⌿(⌽⎕UCS ⍵),0}
	map←256⍴⊂0⍴⊂'' ''
	ins←{
		mp←map⊃⍨bx←hash ⍺
		(⊂⍺)∊⊃¨mp:(bx⊃map)[(⊃¨mp)⍳⊂⍺]←⊂⍺ ⍵
		(bx⊃map),←⊂⍺ ⍵
		0
	}
	rem←{(bx⊃map)←(~(⊃¨m)∊⊂⍵)⌿m←map⊃⍨bx←hash ⍵}
	_←{'-'=⊃⌽⍵:rem ¯1↓⍵ ⋄ ins⌿'='(≠⊆⊢)⍵}¨','(≠⊆⊢)⍵
	+⌿∊(1+⍳≢map){0=≢⍵:0 ⋄ ⍺×(1+⍳≢⍵)×⍎¨⊃∘⌽¨⍵}¨map
	+⌿hash¨','(≠⊆⊢)⍵
}
 
day16_p1←{
	R L U D←(0 1)(0 ¯1)(¯1 0)(1 0)
	mr←∊∘(⍸'/'=⍵) ⋄ ml←∊∘(⍸'\'=⍵) ⋄ sv←∊∘(⍸'|'=⍵) ⋄ sh←∊∘(⍸'-'=⍵)
	seen←⍬
	step←{
		seen,←⊂nrg pos dir←⍵
		pos←dir+pos ⋄ pos dir⌿⍨←⊂∧⌿¨(pos≥0)∧pos<⊂⍴nrg ⋄ pos dir⌿⍨←⊂≠pos,¨dir
		nrg[pos]←1
		dir[i]←U D R L[R L U D⍳dir[i←⍸mr pos]]
		dir[i]←D U L R[R L U D⍳dir[i←⍸ml pos]]
		pos dir⌿⍨←⊂1+m←sv pos ⋄ i←⍸(1+m)⌿m ⋄ dir[i]←(≢i)⍴U D
		pos dir⌿⍨←⊂1+m←sh pos ⋄ i←⍸(1+m)⌿m ⋄ dir[i]←(≢i)⍴L R
		nrg pos dir
	}
	nrg pos dir←step⍣{(⊂⍺)∊seen}((⍴⍵)⍴0)(,⊂0 ¯1)(,⊂R)
	+⌿,nrg
}

day16_p2←{
	R L U D←(0 1)(0 ¯1)(¯1 0)(1 0)
	mr←∊∘(⍸'/'=⍵) ⋄ ml←∊∘(⍸'\'=⍵) ⋄ sv←∊∘(⍸'|'=⍵) ⋄ sh←∊∘(⍸'-'=⍵) ⋄ shp←⍴⍵
	step←{
		pos dir path←⍵ ⋄ pos←dir+pos
		pos dir⌿⍨←⊂∧⌿¨(pos≥0)∧pos<⊂shp
		pos dir⌿⍨←⊂~path[i←(R L U D⍳dir),¨pos] ⋄ path[i]←1
		dir[i]←U D R L[R L U D⍳dir[i←⍸mr pos]]
		dir[i]←D U L R[R L U D⍳dir[i←⍸ml pos]]
		pos dir⌿⍨←⊂1+m←sv pos ⋄ i←⍸(1+m)⌿m ⋄ dir[i]←(≢i)⍴U D
		pos dir⌿⍨←⊂1+m←sh pos ⋄ i←⍸(1+m)⌿m ⋄ dir[i]←(≢i)⍴L R
		pos dir path
	}
	nrg←{pos dir path←step⍣{0=≢⊃⍺}(,⊂⍺)(⊂⍵)(0⍴⍨4,shp) ⋄ +⌿,∨⌿path}
	h w←shp
	l←((⍳h),¨¯1)nrg¨⊂R ⋄ r←((⍳h),¨w)nrg¨⊂L ⋄ t←(¯1,¨⍳w)nrg¨⊂D ⋄ b←(h,¨⍳w)nrg¨⊂U
	⌈⌿l,r,t,b
}

day17_p1←{
         N S E W←(¯1 0)(1 0)(0 1)(0 ¯1)
         unv←(4 4,⍴⍵)⍴1 ⋄ heat←(4 4,⍴⍵)⍴⌊⌿⍬ ⋄ hlm←⍎¨⍵
         smp←(N E W)(S E W)(E N S)(W N S)
         dmp←(0 2 3)(1 2 3)(2 0 1)(3 0 1)
         step←{
                 dir←⊃¨cur←⍵[⊃⍋heat[⍵]]
                 nxt←(dir⊃dmp),¨(1+1 0 0×1⊃¨cur),¨(2↓¨cur)+dir⊃smp
                 nxt⌿⍨←∧⌿¨(nxt≥0)∧(nxt<⊂⍴unv) ⋄ nxt⌿⍨←unv[nxt]
                 heat[nxt]⌊←heat[cur]+hlm[2↓¨nxt]
                 unv[cur]←0
                 (⍵~cur),nxt
         }
         heat[;0;0;0]←0
         _←step⍣{∨⌿~,((⍴hlm)-1)⌷[2 3]unv}⍸0=heat
         ⌊⌿,((⍴hlm)-1)⌷[2 3]heat
}

day17_p2←{
	hlm←,⍎¨⍵ ⋄ h w←⍴⍵ ⋄ N S E W←⍳4 ⋄ off←(-w)w 1 ¯1 ⋄ end←(≢,⍵)-1 ⋄ min←((≢,⍵),4,11)⍴⌊⌿⍬
	{
		pos dir len htl←⍵
		0=≢pos:⌊⌿,min[end;;4+⍳7]
		nd←↑(N E W)(S E W)(E N S)(W N S)[dir]
		np←pos+[0]off[nd]
		nl←((⍪1+len),1),1
		nh←3/⍪htl
		m←~(len∘.<0 4 4)∨(nl>10)∨(np<0)∨(np>end)∨((nd=E)∧[0]0=w|pos+1)∨((nd=W)∧[0]0=w|pos)
		np nd nl nh←(,m)∘⌿∘,¨np nd nl nh
		nh+←hlm[np]
		np nd nl nh i⌿⍨←⊂(min[i←np,¨nd,¨nl]>nh)∧∧/nh∘.<⌊⌿min[end;;4+⍳7]
		np nd nl nh i⌷⍨←⊂⊂⍋nh
		np nd nl nh i⌿⍨←⊂≠np,nd,⍪nl
		min[i]←nh
		∇ np nd nl nh
	}(0 0)(1 2)(0 0)(0 0)
}

day18_p1←{
	h w←⍴⍵ ⋄ dir len color←↓⍉' '(≠⊆⊢)⍤1⊢⍵ ⋄ dir←'UDLR'⍳∊dir ⋄ len←⍎¨len ⋄ color←color
	off←(¯1 0)(1 0)(0 ¯1)(0 1)
	e←0≠⍸⍣¯1↓{⍵[⍋⍵;]}(|⌊⌿e)+[1]e←↑⊃{d l←⍺ ⋄ ⍵,(⊢⌿⍵)+off[d]×1+⍳l}⌿(⌽dir,¨len),⊂,⊂(0 0)
	f←{⍵×~(~e)∧∨/,⍤2⊢0={⍵}⌺3 3⊢⍵}⍣≡e+1
	+⌿,f≠0
}

day18_p2←{
	h w←⍴⍵ ⋄ dir len color←↓⍉' '(≠⊆⊢)⍤1⊢⍵ ⋄ color←⍉2↓¯1↓⍉↑color
	len dir←(16⊥⍉'0123456789abcdef'⍳⎕C 5↑[1]color)('0123'⍳∊¯1↑[1]color)
	off←(0 1)(1 0)(0 ¯1)(¯1 0)
	pts←⊃{⍵,(⊢⌿⍵)+l×off⌷⍨⊃d l←⍺}⌿(⌽dir,¨len),⊂,⊂0 0
	1+(+⌿len⌿⍨dir≤1)+|+⌿2÷⍨2-.×∘⌽⌿pts
}

day19_p1←{
	input←' '⍪⍵
	rules parts←1↓¨input⊂[0]⍨input∧.=' '
	parts←⎕D(⍎¨∊⍨⊆⊢)⍤1⊢parts
	rules←'{' '\,' '}'⎕R'←{x m a s←⍵ ⋄ ' '⍵ ⋄ ' '⍵}'⍤1⊢rules
	A←{1} ⋄ R←{0}
	eval←⎕NS,¨'A' 'R'
	_←{_←eval.⍎⍵ ⋄ 0}⍤1⊢rules
	+⌿,(eval.in⍤1⊢parts)⌿parts
}

day19_p2←{
	input←' '⍪⍵
	rules parts←1↓¨input⊂[0]⍨input∧.=' '
	rules←'{' '<' '>' ':' '\,' '\}'⎕R'←{(' ' LT ' ' GT ' ' merge ' ')comb(' ')⍵}'⍤1⊢rules
	ns←⎕NS ⍬
	ns.(x m a s)←⍳4
	ns.A←{(×⌿¯1+|-/⍵)⍵}
	ns.R←{0 ⍵}
	ns.comb←{c1 r1←⍺⍺ ⍵ ⋄ c2 r2←⍵⍵ r1 ⋄ (c1+c2)r2}
	ns.merge←{c r←⍵⍵ ⍺⍺ ⍵ ⋄ c(1 ⍺⍺ ⍵)}
	ns.GT←{⍺←0 ⋄ ⍺:⍺⍺ LT(⍵⍵+1)⊢⍵ ⋄ r←⍵ ⋄ r[⍺⍺;0]⌈←⍵⍵ ⋄ r}
	ns.LT←{⍺←0 ⋄ ⍺:⍺⍺ GT(⍵⍵-1)⊢⍵ ⋄ r←⍵ ⋄ r[⍺⍺;1]⌊←⍵⍵ ⋄ r}
	_←{_←ns.⍎⍵ ⋄ 0}⍤1⊢rules
	⊃ns.in 4 2⍴0 4001
}
 
day20_p1←{
	type←'b%&'⍳⍵[;0]
	mod dst←↓⍉{(⊃x)(1↓x)⊣x←(⎕C ⎕A)(∊⍨⊆⊢)⍵}⍤1⊢⍵
	dst←mod∘⍳¨dst ⋄ inp←{⍸⍵∘∊¨dst}¨⍳≢dst ⋄ b←⊃⍸type=0
	pc←0 0
	pulse←{
		str src tgt←⍵
		tgt≥≢type:⍺ ⍬
		typ←type[tgt] ⋄ nt←(tgt⍴⍨≢nt),¨nt←tgt⊃dst
		(typ=0):⍺(str,¨nt)
		(typ=1)∧str:⍺ ⍬
		(typ=1):(1≠@tgt⊢⍺)((1≠⍺[tgt]),¨nt)
		s←str@(⊂tgt,src⍳⍨tgt⊃inp)⊢⍺
		s((~∧⌿tgt⊃s),¨nt)
	}
	push←{0=≢⍵:⍺ ⋄ pc[⊃⊃⍵]+←1 ⋄ ns np←⍺ pulse⊃⍵ ⋄ ns ∇(1↓⍵),np}
	ip←,⊂0 ¯1 b
	is←0@(⍸type≠2)⊢0×inp
	×⌿⎕←pc⊣ip push⍨⍣1000⊢is
}
 
day20_p2←{
	⍝ Manually extracted the following state from analysis of the code.
	
	⍝ state←0 1 0 0 0 0 1 1 1 1 1 1 1 1 1 0 1 0 0 0 0 1 1 1 1 1 1 1 1 1 1 0 1 1 1 1 1 0 1 0 1 1 0 0 0 1 1 1 1 0 1 0 1 1 0 1 1 1
	⍝ 2⊥⍉4 12⍴10↓state
	⍝ ∧⌿2⊥⍉4 12⍴10↓state
	
	⍝ broadcaster -> kg, dz, ff, bq
	⍝ &gf -> rx
	⍝ 
	⍝ &qk -> gf
	⍝ &zs -> gf
	⍝ &kr -> gf
	⍝ &kf -> gf
	⍝ 
	⍝ &qr -> dp, bq, nd, rz, mg, qk, mh
	⍝ &cx -> ff, vx, zs
	⍝ &bf -> dz, zg, kr, sl, fk, kp, dc
	⍝ &gm -> kg, kf, fq, nc, lf
	⍝ 
	⍝ %sf -> qr
	⍝ %xk -> qr, sf
	⍝ %pf -> xk, qr
	⍝ %zz -> pf, qr
	⍝ %jx -> qr, zz
	⍝ %nd -> jx
	⍝ %tj -> nd, qr
	⍝ %rz -> tj
	⍝ %mh -> rz
	⍝ %dp -> mh
	⍝ %mg -> dp
	⍝ %bq -> qr, mg
	⍝ 
	⍝ %xt -> cx
	⍝ %rj -> xt, cx
	⍝ %sx -> cx, rj
	⍝ %mf -> cx, sx
	⍝ %tz -> mf, cx
	⍝ %hm -> cx, tz
	⍝ %kt -> hm, cx
	⍝ %jp -> cx, kt
	⍝ %cv -> jp, cx
	⍝ %vx -> cv
	⍝ %vl -> vx, cx
	⍝ %ff -> vl, cx
	⍝ 
	⍝ %dl -> bf
	⍝ %hg -> dl, bf
	⍝ %bb -> hg, bf
	⍝ %zg -> bb
	⍝ %pz -> zg, bf
	⍝ %kp -> pz
	⍝ %jb -> bf, kp
	⍝ %rp -> jb, bf
	⍝ %sl -> rp
	⍝ %fk -> sl
	⍝ %dc -> fk
	⍝ %dz -> bf, dc
	⍝ 
	⍝ %vq -> gm
	⍝ %bh -> vq, gm
	⍝ %qn -> gm, bh
	⍝ %fq -> qn
	⍝ %hr -> fq, gm
	⍝ %lf -> hr
	⍝ %ls -> lf, gm
	⍝ %gb -> ls, gm
	⍝ %nc -> gb
	⍝ %vv -> nc, gm
	⍝ %pt -> gm, vv
	⍝ %kg -> gm, pt
	
	in←⍵⌿⍨~⍵∧.=' '
	type←'b%&'⍳in[;0]
	mod dst←↓⍉{(⊃x)(1↓x)⊣x←(⎕C ⎕A)(∊⍨⊆⊢)⍵}⍤1⊢in
	dst←mod∘⍳¨dst ⋄ inp←{⍸⍵∘∊¨dst}¨⍳≢dst ⋄ b←⊃⍸type=0
	hlc←0 0 ⋄ hst←(≢dst)⍴0
	pulse←{
		str src tgt←⍵
		hlc[str]+←1
		⎕←((1+src)⊃(⊂'BT'),mod),'→',('LH'[str]),'→',tgt⊃mod,⊂'rx '
		tgt≥≢type:⍺ ⍬
		typ←type[tgt] ⋄ nt←(tgt⍴⍨≢nt),¨nt←tgt⊃dst
		(typ=0):⍺(str,¨nt)
		(typ=1)∧str:⍺ ⍬
		(typ=1):(1≠@tgt⊢⍺)((1≠⍺[tgt]),¨nt)
		s←(~∧⌿⍺[tgt⊃inp])@tgt⊢⍺
		s(s[tgt],¨nt)
	}
	push←{0=≢⍵:⍺ ⋄ ns np←⍺ pulse⊃⍵ ⋄ hst+←ns≠hst ⋄ ns ∇(1↓⍵),np}
	ip←,⊂0 ¯1 b
	is←(≢dst)⍴0
	⍺←is
	⎕←st←ip{c s←⍵ ⋄ (1+c)(s push ⍺)}⍣1⊢0 ⍺
	(×⌿,⊢)hlc
}

day21_p1←{
	map←'#.S'⍳⍵
	step←{(1⌊map)+(⍵≠0)∧∨/(⊂1 3 5 7)⌷[2],⍤2⊢{⍵}⌺3 3⊢2=⍵}
	+⌿,'S'='#.S'[step⍣64⊢map]
}

day22_p1←{
	brks←⊂[1 2]{⍵[⍒⌊/⍵[;;2];;]}(≢⍵)2 3⍴⍎¨⎕D(∊⍨⊆⊢)⍤1⊢⍵
	xy←{⍵[;0 1]}¨brks ⋄ z←{⍵[;2]}¨brks
	ovlp←(∘.≠⍨⍳≢z)∧∘.{∧⌿((0⌷⍺)≤1⌷⍵)∧(1⌷⍺)≥0⌷⍵}⍨xy
	det←ovlp∧∧⍀∘.≠⍨⍳≢z
	_←{(⍵⊃z)←(0,|-⌿⍵⊃z)+1+0⌈⌈⌿∊det[⍵;]⌿z}¨⌽⍳≢z
	hz←⌈⌿¨z ⋄ lz←⌊⌿¨z
	supp←ovlp∧hz∘.=lz-1
	eqlv←(∘.≠⍨⍳≢z)∧∘.=⍨hz
	+⌿eqlv{⍵≡⍵∧∨⌿⍺⌿supp}⍤1⊢supp
}

day23_p1←{map←⍵
	open←⍸⍵≠'#'
	dir←(¯1 0)(1 0)(0 ¯1)(0 1) ⋄ U D L R←⊂¨dir
	nxt←{⊂⍵⌿⍨⍵∊open}⍤1⊢(⍳⍴⍵)∘.+dir
	nxt[i]←,∘⊂¨R+i←⍸⍵='>'
	nxt[i]←,∘⊂¨D+i←⍸⍵='v'
	start end←⊂¨(⊣⌿,⊢⌿),open
	{⍺←(⍴map)⍴0
		⍵≡end:+⌿,⍺
		v←1@⍵⊢⍺
		0=+⌿x⌿⍨←~v[x←⊃nxt[⍵]]:0
		⌈⌿v∘∇∘⊂¨x
	}start
}

day23_p2←{map←⍵
	open←⍸⍵≠'#'
	dir←(¯1 0)(1 0)(0 ¯1)(0 1) ⋄ U D L R←⊂¨dir
	nxt←(⍴⍵)⍴(,⍵≠'#')⍀{⊂⍵⌿⍨⍵∊open}⍤1⊢open∘.+dir
	start end←(⊣⌿,⊢⌿),open
	follow←{⍺←1 ⋄ cur←0⌷⍵ ⋄ 2≠≢x←⊃nxt[cur]:(forks⍳cur)⍺ ⋄ (⍺+1)∇(x~⍵),1↑⍵}
	fkid fkct←↓⍉↑{↓⍉↑follow¨(⊃nxt[⊂⍵]){⍺ ⍵}¨⊂⍵}¨forks←open⌿⍨2≠≢¨nxt[open]
	start end←forks⍳start end ⋄ max←0
	{⍺←(≢forks)⍴0
		⍵≡end:max⌈←{⍵>max:⎕←⍵ ⋄ ⍵}+⌿,⍺
		0=+⌿x⌿⍨←m←0=⍺[x←⍵⊃fkid]:0
		v←x{⍵@⍺⊢v}¨m⌿⍵⊃fkct⊣v←⍺
		⌈⌿v ∇¨x
	}start
}

day24_p1←{
	low←200000000000000 ⋄ high←400000000000000
	px py pz vx vy vz←↓⍉↑{('-',⎕D)(⍎¨∊⍨⊆⊢)⍵}⍤1⊢⍵
	intsct←{⎕FR←1287
		Ap Av←⍺ ⋄ A0←Ap ⋄ A1←Ap+Av ⋄ A←A0-A1
		Bp Bv←⍵ ⋄ B0←Bp ⋄ B1←Bp+Bv ⋄ B←B0-B1
		0=d←A-.×⌽B:0
		x y←d÷⍨⊃B A-.×(A0-.×⌽A1)(B0-.×⌽B1)
		∨⌿∊0≥((⊂x y)-Ap Bp)÷Av Bv:0
		∨⌿(x y<low)∨high<x y:0
		1
	}
	lines←(↓px,⍪py){⍺ ⍵}¨↓vx,⍪vy
	test←{cur∘←⍺ ⍵ ⋄ (⍺⊃lines)intsct ⍵⊃lines}
	⊃⊃{((⊃⍵)++⌿⍺ test¨1↓⍵),⍺,1↓⍵}⌿(⍳≢lines),0
}
 
:EndNamespace
